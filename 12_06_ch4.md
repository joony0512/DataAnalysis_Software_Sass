## %let

```sass
%let month = 2;
%let year = 2007;
data orders;
 	keep order_date order_type quantity total_retail_price;
	set UE.order_fact end=final;
	where year(order_date)=&year and month(order_date) = &month;
	if order_type = 3 then Number +1;
	if final then do;
		put Number = ;
		if Number = 0 then do;
		 	%let foot = No Internet Orders;
		end;
		else do;
			%let foot = Some Internet Orders;
		end;
	end;
run;

proc print data = orders;
	title "orders for &month-&year";
	footnote "&foot";
run;
```

**orders for 2-2007**

Some Internet Orders

## symputx

```sass
%let month = 2;
%let year = 2007;
data orders;
 	keep order_date order_type quantity total_retail_price;
	set UE.order_fact end=final;
	where year(order_date)=&year and month(order_date) = &month;
	if order_type = 3 then Number +1;
	if final then do;
		put Number = ;
		if Number = 0 then do;
		 	call symputx('foot', 'No Internet Orders');
		end;
		else do;
			call symputx('foot', 'Some Internet Orders');
		end;
	end;
run;
proc print data = orders;
	title "orders for &month-&year";
	footnote "&foot";
run;
```

**orders for 2-2007**

No Internet Orders

- symput이 let 보다 세다

```sass
data orders;
 	keep order_date order_type quantity total_retail_price;
	set UE.order_fact end=final;
	where year(order_date)=&year and month(order_date) = &month;
	if order_type = 3 then Number +1;
	if final then do;
		put Number = ;
		if Number = 0 then do;
		 	call symputx('foot', 'No Internet Orders');
		end;
		else do;
			call symputx('foot', 'Some Internet Orders');
		end;
	end;
run;

data _null_;
	call symputx('foot', 'No internet orders');
	%let foot=Some internet orders;
run;

proc print data = orders;
	title "orders for &month-&year";
	footnote "&foot";
run;
```

**orders for 2-2007**

No internet orders

- _*null*_ : table 만들지 않고 변수 생성가능
    - DATA  _*null*_ 이라고 입력하면 테이블이 생성되지 않습니다. SYMPUT 명령어는 %LET과 달리 단독으로 쓸 수 없어 DATA STEP 내에서 SYMPUT 명령어를 사용합니다.
    - 그러나 굳이 매크로 변수 때문에 테이블을 생성할 필요는 없으므로 CALL SYMPUT 사용 시 테이블을 생성하지 않는 DATA _NULL_로 자주 이용합니다.

```sass
data _null_;
	date = today();
	amount = 12000000;
	number = 50;
	call symputx('dat',put(date,date9.));
	call symputx('avg',put(amount/number, dollar10.));
run;
%put &dat;
%put &avg;
```

86   %put &dat;
20DEC2022
87   %put &avg;
$240,000

- where 절에서 참조

```sass
%let custID = 9;

data _null_;
	set UE.customer;
	where customer_ID = &custID;
	call symputx('name', Customer_Name);
run;

proc print data = UE.order_fact;
where customer_ID = &custID;
var order_date order_type quantity total_retail_price;
title1 " Customer Number : &custID";
title2 "Customer Name: &name";
run;
```

**Customer Number : 9**

**Customer Name: Cornelia Krahl**

- name+customer_id를 합친 매크로변수만듬

```sass

data _null_;
set UE.customer;
call symputx('name'||left(Customer_ID), Customer_name);
run;
%put _user_;

```

- _user _ : Display user-defined macro variables.

GLOBAL NAME Cornelia Krahl
GLOBAL NAME10 Karen Ballinger
GLOBAL NAME1033 Selim Okay
GLOBAL NAME11 Elke Wallstab
GLOBAL NAME1100 Ahmet Canko
GLOBAL NAME111 Karolina Dokter
GLOBAL NAME11171 Bill Cuddy
GLOBAL NAME12 David Black
GLOBAL NAME12386 Avinoam Zweig
GLOBAL NAME13 Markus Sepke
GLOBAL NAME14104 Avinoam Zweig
GLOBAL NAME14703 Eyal Bloch
GLOBAL NAME16 Ulrich Heyde
GLOBAL NAME1684 Carglar Aydemir
GLOBAL NAME17 Jimmie Evans
GLOBAL NAME17023 Susan Krasowski
GLOBAL NAME171 Robert Bowerman
GLOBAL NAME18 Tonie Asmussen
GLOBAL NAME183 Duncan Robertshawe
GLOBAL NAME19 Oliver S. F廻ling
GLOBAL NAME19444 Avinoam Zweig
GLOBAL NAME195 Cosi Rimmington
GLOBAL NAME19873 Avinoam Tuvia
GLOBAL NAME20 Michael Dineley

```sass
%put &NAME9;
```

%put &NAME9;
Cornelia Krahl

```sass

%let custID = 9;

data _null_;
set UE.customer;
call symputx('name'||left(Customer_ID), Customer_name);
run;

proc print data = UE.order_fact;
where customer_ID = &custID;
var order_date order_type quantity total_retail_price;
title1 " Customer Number : &custID";
title2 "Customer Name: &&name&custID";
run;
```

**Customer Number : 9**

**Customer Name: Cornelia Krahl**

## into

- INTO 절은 매크로 변수 생성

```sass
proc sql noprint;
select sum(total_retail_price) format=dollar8.
into : total 
from UE.order_fact
where year(order_date) = 2007 and order_type = 3;
quit;
%put &total;
```

- total이라는 매크로 변수 생성

%put &total;
$4,124

- INTO 절은 여러 개의 매크로 변수 생성 가능
- Example: 2007년 매출 상위 3개의 날짜와 금액으로 매크로 변수 생성

```sass
title 'Top 2007 Sales';
proc sql outobs=3 ;
select total_retail_price, order_date format = mmddyy10.
into : price1-:price3, :date1-:date3
from UE.order_fact
where year(order_date) = 2007
order by total_retail_price desc;
quit;
```

**Top 2007 Sales**

- Example: 모든 고객 국가 목록이 포함된 매크로 변수 생성.
    - 국가 코드는 쉼표와 공백으로 구분

```sass
proc sql noprint;
select distinct country into : countries
separated by ', '
from UE.customer;
quit;
%put Customer Countries:&Countries;
```

%put Customer Countries:&countries;
Customer Countries:AU, CA, DE, IL, TR, US, ZA

```sass
%macro deleteALL;
proc sql noprint;
select name into : vars separated by ' '
from dictionary.macros
where scope ='GLOBAL';
quit;
%symdel &vars;
%mend deleteALL;
%deleteALL
```

## 과제 5

![Untitled](https://s3-us-west-2.amazonaws.com/secure.notion-static.com/7957a6b4-74f5-489b-a452-bcfa32cac445/Untitled.png)

1. 첫번째 sql 프로그램을 수정하여 새로운 매크로 변수 new를 생성한다. 생성되
는 매크로변수 new에서는 total_retail_price가 가장 큰 세 customer의
customer_id를 저장한다. 이 때 저장되는 값은 ,와 공백으로 분리되게 한다.
2. 생성된 매크로 변수 new를 이용하여 두번째 proc print 스텝을 수행한다. 수행
결과가 다음과 같음을 확인한다.
