# Ch5

수평결합, 수직결합

- inner join : 교집합
- outer join : 한테이블을 기준으로 일치하는행과 일치하지 않는 행 반환가능
    - left, full, right
- catesian product :  가능한 모든 조합 반환

### 데이터 만들기

```sass
data alpha;
input x a$;
cards;
1 a
4 d
2 b
;
run;
NOTE: 데이터셋 WORK.ALPHA은(는) 3개 관측값과 2개 변수를 가집니다.

data alpha;
input x a$@@;
cards;
1 a 4 d 2 b
;
run;
NOTE: INPUT 문이 행의 끝에 도달했을 때 SAS는 다음 행으로 이동합니다.
NOTE: 데이터셋 WORK.ALPHA은(는) 3개 관측값과 2개 변수를 가집니다.

data beta;
input x b$@@;
cards;
2 x 3 y 5 V 6 L
;
run;
NOTE: INPUT 문이 행의 끝에 도달했을 때 SAS는 다음 행으로 이동합니다.
NOTE: 데이터셋 WORK.BETA은(는) 4개 관측값과 2개 변수를 가집니다.

data gamma;
input x c$@@;
cards;
5 a 6 b 8 c 9 d
;
run;

```

- catesian product

```sass
proc sql;
select *
from alpha, beta;
quit;

proc sql;
select *
from  alpha, beta, gamma;
quit;
```

- inner join

```sass
proc sql;
select *
from alpha, beta
where alpha.x = beta.x;
quit;
//결과
x a x b 
2 b 2 x

proc sql;
select alpha.x, a, b
from alpha, beta
where alpha.x = beta.x;
quit;
//결과
x a b 
2 b x
```

- 중복키 있을때 : 중복을 포함하여 일치하는 키가 있는 모든 행의 조합을 표시

```sass
data three;
input x a$@@;
cards;
1 a1 1 a2 2 b1 2 b2 4 d 
;
run;

data four;
input x b$@@;
cards;
2 x1 2 x2 3 y 5 v
;
run;

proc sql;
select *
from three, four
where three.x = four.x;
quit;
//결과
x a x b 
2 b1 2 x1 
2 b1 2 x2 
2 b2 2 x1 
2 b2 2 x2
```

- merge는 카테시안 곱을 하지 않고 가로결합만 한다

```sass
data new;
merge three four;
by x;
if x=2;
run;

proc sql;
select *
from new;
quit;

//결과
x a b 
2 b1 x1 
2 b2 x2
```

- Example: Display the names, job codes, and ages
of all New York employees.
▪ Employee names are found in the
airline.staffmaster table.
▪ Employee job codes and birth dates are found
in the airline.payrollmaster table.
    
    ```sass
    proc sql;
    select firstname, lastname, jobcode, int((today() - dateofbirth)/365.25) as ages
    from a.staffmaster, a.payrollmaster 
    where staffmaster.EmpID = payrollmaster.EmpID and state = 'NY' ;
    quit;
    ```
    

# left outer join

```sass
proc sql;
select *
from alpha left join beta on alpha.x = beta.x;
quit;

```

| x | a | x | b |
| --- | --- | --- | --- |
| 1 | a | . |  |
| 2 | b | 2 | x |
| 4 | d | . |  |

# right outer join

```sass
proc sql;
select *
from alpha right join beta on alpha.x = beta.x;
quit;
```

| x | a | x | b |
| --- | --- | --- | --- |
| 2 | b | 2 | x |
| . |  | 3 | y |
| . |  | 5 | V |
| . |  | 6 | L |

# full outer join

```sass
proc sql;
select *
from alpha full join beta on alpha.x = beta.x;
quit;
```

| x | a | x | b |
| --- | --- | --- | --- |
| 1 | a | . |  |
| 2 | b | 2 | x |
| . |  | 3 | y |
| 4 | d | . |  |
| . |  | 5 | V |
| . |  | 6 | L |

# left outer join

```sass
proc sql;
select marchflights.Date, marchflights.FlightNumber label='Flight Number'
	,marchflights.Destination label='Left',flightdelays.Destination label='Right',
Delay
from a.marchflights left join a.flightdelays 
	on marchflights.flightnumber = flightdelays.flightnumber and marchflights.date = flightdelays.date
order by Delay;
quit;

proc sql;
select a.Date, a.FlightNumber label='Flight Number'
	,a.Destination label='Left',b.Destination label='Right',
Delay
from a.marchflights as a left join a.flightdelays as b
	on a.flightnumber = b.flightnumber and a.date = b.date
order by Delay;
quit;
```

| Date | Flight Number | Left | Right | Delay |
| --- | --- | --- | --- | --- |
| 11MAR2000 | 290 | WAS |  | . |
| 25MAR2000 | 872 | LAX |  | . |
| 08MAR2000 | 182 | YYZ |  | . |
| 03MAR2000 | 416 | WAS |  | . |
| 17MAR2000 | 182 | YYZ |  | . |
| 29MAR2000 | 829 | WAS |  | . |
| 16MAR2000 | 622 | FRA |  | . |
| . | 132 | YYZ |  | . |
| 14MAR2000 | 271 | CDG |  | . |
| 22MAR2000 | 183 | WAS |  | . |
| 27MAR2000 | 982 | DFW |  | . |
| 11MAR2000 | 202 | ORD |  | . |
| 02MAR2000 | 387 | CPH | CPH | -10 |
| 10MAR2000 | 523 | ORD | ORD | -10 |
| 25MAR2000 | 829 | WAS | WAS | -10 |
| 07MAR2000 | 523 | ORD | ORD | -10 |
| 09MAR2000 | 821 | LHR | LHR | -10 |
| 14MAR2000 | 829 | WAS | WAS | -10 |
| 18MAR2000 | 219 | LHR | LHR | -10 |
| 21MAR2000 | 183 | WAS | WAS | -9 |
| 19MAR2000 | 132 | YYZ | YYZ | -9 |
| 02MAR2000 | 821 | LHR | LHR | -9 |
| 27MAR2000 | 182 | YYZ | YYZ | -9 |
| 09MAR2000 | 829 | WAS | WAS | -9 |
| 15MAR2000 | 821 | LHR | LHR | -9 |
| 13MAR2000 | 202 | ORD | ORD | -9 |
| 15MAR2000 | 219 | LHR | LHR | -9 |
| 14MAR2000 | 308 | ORD | ORD | -9 |
| 18MAR2000 | 182 | YYZ | YYZ | -9 |
| 23MAR2000 | 982 | DFW | DFW | -9 |
| 14MAR2000 | 387 | CPH | CPH | -9 |
| 28MAR2000 | 182 | YYZ | YYZ | -9 |
| 01MAR2000 | 183 | WAS | WAS | -8 |
| 24MAR2000 | 182 | YYZ | YYZ | -8 |
| 21MAR2000 | 439 | LAX | LAX | -8 |
| 05MAR2000 | 523 | ORD | ORD | -8 |
| 04MAR2000 | 290 | WAS | WAS | -8 |
| 15MAR2000 | 114 | LAX | LAX | -8 |
| 08MAR2000 | 132 | YYZ | YYZ | -8 |
| 17MAR2000 | 387 | CPH | CPH | -8 |
| 26MAR2000 | 982 | DFW | DFW | -8 |

## COALESCE function : 같은 열 결합

```sass
proc sql ;
select coalesce(alpha.x, beta.x) label = 'x', a, b
from alpha full join beta on alpha.x = beta.x ;
quit;
```

| x | a | b |
| --- | --- | --- |
| 1 | a |  |
| 2 | b | x |
| 3 |  | y |
| 4 | d |  |
| 5 |  | V |
| 6 |  | L |

# inline view (from 절의 서브쿼리)

```sass
proc sql;
select destination, max - min label ='Range of Delay'
from (select destination, avg(delay) as average format =3.0 label = 'Average Delay',
	min(Delay) as min format = 4.0 label = "Minimum Delay",
	max(Delay) as max format = 3.0 label = 'Maximum Delay'
from a.flightdelays
group by destination)
order by 2;
quit;
```

| Destination | Range of Delay |
| --- | --- |
| YYZ | 23 |
| WAS | 25 |
| DFW | 29 |
| ORD | 29 |
| LAX | 35 |
| CPH | 36 |
| LHR | 40 |
| FRA | 41 |
| CDG | 45 |
- complex query

```sass
#1 코펜하겐 비행기에 2000년 3월4일날 탑승한 승무원찾기

proc sql;
select EmpID
from a.flightschedule
where destination = 'CPH' and date = '04mar2000'd;
quit;

#2 승무원의 state와 jobcategory찾기

proc sql;
select substr(p.Jobcode, 1, 2) as JobCategory, s.state
from a.staffmaster as s, a.payrollmaster as p
where s.EmpID = p.EmpID and s.EmpID
	in (select EmpID
	from a.flightschedule
	where destination = 'CPH' and date = '04mar2000'd);
quit;

#3 state와 jobcategory를 기반으로 승무원 감독관의 직원번호 찾기

proc sql;
select EmpID 
from a.supervisors as m, 
	(select substr(p.Jobcode, 1, 2) as JobCategory, s.state
	from a.staffmaster as s, a.payrollmaster as p
	where s.EmpID = p.EmpID and s.EmpID
	in (select EmpID
		from a.flightschedule
		where destination = 'CPH' and date = '04mar2000'd)) as c
where m.JobCategory = c.JobCategory
	and m.State = c.State;
quit;

#4 승무원 감독관의 직원번호를 바탕으로 관리자 이름 찾기

proc sql;
select  firstname,lastname
from a.staffmaster 
where EmpID in
	(select EmpID 
	from a.supervisors as m, 
		(select substr(p.Jobcode, 1, 2) as JobCategory, s.state
		from a.staffmaster as s, a.payrollmaster as p
		where s.EmpID = p.EmpID and s.EmpID
			in (select EmpID
			from a.flightschedule
			where destination = 'CDG' and date = '04mar2000'd)) as c
	where m.JobCategory = c.JobCategory
	and m.State = c.State);
quit;
```
