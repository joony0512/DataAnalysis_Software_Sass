# ch6

## except : 두번째 테이블에서 찾을 수 없는 첫번째테이블의 고유한 행

```sass
data one;
input x a$@@;
cards; 
1 a 1 a 1 b 2 c 3 v 4 e 6 g
;
run;
data two;
input x b$@@;
cards;
1 x 2 y 3 z 3 v 5 w
;
run;
proc sql;
select *
from one
except
select *
from two;
quit;

1. all 을 안썼기 때문에 one 테이블에서 중복 된 값 1 a 없어짐 --> 자동
2. table two에서 3 v를 갖고 있어서 one에서 없어짐
3. 열이름은 첫테이블 기준으로 정해짐
```

| x | a |
| --- | --- |
| 1 | a |
| 1 | b |
| 2 | c |
| 4 | e |
| 6 | g |

```sass
proc sql;
select *
from one
except all
select *
from two;
quit;

//all써서 첫테이블의 중복값 안지움
```

| x | a |
| --- | --- |
| 1 | a |
| 1 | a |
| 1 | b |
| 2 | c |
| 4 | e |
| 6 | g |

# corr : 위치대신 열의 이름별로 열을 중첩, 없는 열 지움

```sass
proc sql;
select *
from one
except corr
select *
from two;
quit;
// 1. 두테이블이 x열만 이름이 같아서 x열만 남음(corr)
// 2. 두번째 테이블이 갖고 있는 x값 첫테이블에서 다 지움(except)
// 3. 같은 이름의 열이름 붙임
```

x

---

4

---

6

---

```sass
proc sql;
select *
from one
except all corr
select *
from two;
quit; 
//1. corr -> 서로 없는 행 지움
//2. all -> 중복되는거 있으면 맨위꺼만 지움
//3. except -> 첫테이블에 없는거 지움
```

| x |
| --- |
| 1 |
| 1 |
| 4 |
| 6 |

## intersect : 두테이블의 공통 고유행이 선택됨

```sass
proc sql;
select *
from one
intersect
select *
from two;
quit;
//1.첫테이블의 중복행 제거 -->자동
//2.두테이블에서 같은 행 반환
//3.열이름은 첫테이블꺼 씀
```

| x | a |
| --- | --- |
| 3 | v |

```sass
proc sql;
select *
from one
intersect all
select *
from two;
quit;
//1.첫테이블의 중복행 제거안함 (all)
//2.두테이블에서 같은 행 반환
//3.열이름은 첫테이블꺼 씀
```

| x | a |
| --- | --- |
| 3 | v |

```sass
proc sql;
select *
from one
intersect corr
select *
from two;
quit;
//1. 두테이블이 공통으로 갖고 있지 않은 열 없앰(corr)
//2. 중복행 제거-->자동
//2. 두테이블이 공통으로 갖고있는 열의 공통값 반환
```

| x |
| --- |
| 1 |
| 2 |
| 3 |

```sass
proc sql;
select firstname, lastname
from a.staffmaster
intersect all
select firstname, lastname
from a.staffchanges;
quit;
```

| FirstName | LastName |
| --- | --- |
| DIANE | WALTERS |
| KAREN | CARTER |
| NEIL | CHAPMAN |
| RAYMOND | SANDERS |

```sass
proc sql;
select firstname, lastname
from a.staffmaster
where state = 'NY'
intersect all
select firstname, lastname
from a.staffchanges
where state = 'NY';
quit;
```

| FirstName | LastName |
| --- | --- |
| DIANE | WALTERS |
| NEIL | CHAPMAN |
| RAYMOND | SANDERS |
- union : 두테이블의 고유한 행은 모두 열이 겹쳐진 상태로 선택됨
    
    ```sass
    proc sql;
    select *
    from one
    union
    select *
    from two;
    quit;
    
    // 아래로 겹침, 중복행제거
    ```
    
    | x | a |
    | --- | --- |
    | 1 | a |
    | 1 | b |
    | 1 | x |
    | 2 | c |
    | 2 | y |
    | 3 | v |
    | 3 | z |
    | 4 | e |
    | 5 | w |
    | 6 | g |
    
    ```sass
    proc sql;
    select *
    from one
    union all
    select *
    from two;
    quit;
    
    //중복행 제거 안함
    ```
    
    | x | a |
    | --- | --- |
    | 1 | a |
    | 1 | a |
    | 1 | b |
    | 2 | c |
    | 3 | v |
    | 4 | e |
    | 6 | g |
    | 1 | x |
    | 2 | y |
    | 3 | z |
    | 3 | v |
    | 5 | w |
    
    ```sass
    proc sql;
    select *
    from one
    union corr
    select *
    from two;
    quit;
    
    //동일열이름만 합침
    //중복행제거
    ```
    
    | x |
    | --- |
    | 1 |
    | 2 |
    | 3 |
    | 4 |
    | 5 |
    | 6 |
    
    ## 데이터를 세로로 쌓고 싶을때 유용
    
    ```sass
    proc sql;
    title 'Points and Miles Traveled '
    'by Frequent Flyers';
    select 'Total Points Earned :',
    sum(PointsEarned) format=comma12.
    from a.frequentflyers
    union
    select 'Total Points Used :',
    sum(PointsUsed) format=comma12.
    from a.frequentflyers
    union
    select 'Total Miles Traveled:',
    sum(MilesTraveled) format=comma12.
    from a.frequentflyers;
    quit;
    ```
    
    Points and Miles Traveled by Frequent 
    Flyers
    
    ---
    
    |  |  |
    | --- | --- |
    | Total Miles Traveled: | 10,477,963 |
    | Total Points Earned : | 11,083,463 |
    | Total Points Used : | 4,429,670 |
